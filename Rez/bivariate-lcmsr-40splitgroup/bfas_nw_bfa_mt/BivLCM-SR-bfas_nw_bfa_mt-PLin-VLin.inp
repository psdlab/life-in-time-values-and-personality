
title: PxVx Bivariate LCM-SR - 40splitgroup - Linear bfas_nw with Linear bfa_mt;
data: file = /home/jflournoy/code_new/lnt_pxvx/Data/lnt_nat_recalc.tsv;

variable:
names =
subjid
aage
aaspfin
aaspfin_d
abfa_mt
abfa_mt_d
abfas_ac
abfas_ap
abfas_ci
abfas_co
abfas_ea
abfas_ee
abfas_nv
abfas_nv9
abfas_nw
abfas_oi
abfas_oo
abfi_a
abfi_a6
abfi_c
abfi_d_scale
abfi_e
abfi_hp8
abfi_n
abfi_o
abfi_s_scale
ahrz_col
ahrz_col_d
ahrz_ind
ahrz_ind_d
amvi
ausi
ausi_d
avrt_col
avrt_col_d
avrt_ind
avrt_ind_d
bage
baspfin
baspfin_d
bbfa_mt
bbfa_mt_d
bbfas_ac
bbfas_ap
bbfas_ci
bbfas_co
bbfas_ea
bbfas_ee
bbfas_nv
bbfas_nv9
bbfas_nw
bbfas_oi
bbfas_oo
bbfi_a
bbfi_a6
bbfi_c
bbfi_d_scale
bbfi_e
bbfi_hp8
bbfi_n
bbfi_o
bbfi_s_scale
bhrz_col
bhrz_col_d
bhrz_ind
bhrz_ind_d
bmvi
busi
busi_d
bvrt_col
bvrt_col_d
bvrt_ind
bvrt_ind_d
cage
caspfin
caspfin_d
cbfa_mt
cbfa_mt_d
cbfas_ac
cbfas_ap
cbfas_ci
cbfas_co
cbfas_ea
cbfas_ee
cbfas_nv
cbfas_nv9
cbfas_nw
cbfas_oi
cbfas_oo
cbfi_a
cbfi_a6
cbfi_c
cbfi_d_scale
cbfi_e
cbfi_hp8
cbfi_n
cbfi_o
cbfi_s_scale
chrz_col
chrz_col_d
chrz_ind
chrz_ind_d
cmvi
cusi
cusi_d
cvrt_col
cvrt_col_d
cvrt_ind
cvrt_ind_d
dage
daspfin
daspfin_d
dbfa_mt
dbfa_mt_d
dbfas_ac
dbfas_ap
dbfas_ci
dbfas_co
dbfas_ea
dbfas_ee
dbfas_nv
dbfas_nv9
dbfas_nw
dbfas_oi
dbfas_oo
dbfi_a
dbfi_a6
dbfi_c
dbfi_d_scale
dbfi_e
dbfi_hp8
dbfi_n
dbfi_o
dbfi_s_scale
dhrz_col
dhrz_col_d
dhrz_ind
dhrz_ind_d
dmvi
dusi
dusi_d
dvrt_col
dvrt_col_d
dvrt_ind
dvrt_ind_d;

USEVARIABLES =
abfas_nw
bbfas_nw
cbfas_nw
dbfas_nw
abfa_mt
bbfa_mt
cbfa_mt
dbfa_mt
a1
a2
a3
a4
aagegrp
;

missing = all (-9999);

tscores = a1 a2 a3 a4;

grouping = aagegrp (0=earlier, 1=later);

DEFINE:

aagegrp = aage;

cut aagegrp (39);

a1 = aage + 0 - 36;
a2 = aage + 1 - 36;
a3 = aage + 2 - 36;
a4 = aage + 3 - 36;

ANALYSIS:
TYPE=RANDOM;
PROCESSORS = 6; 
model=nocovariances; ! supresses default estimation of certain covs in model;

MODEL:

i_bfas_nw s_bfas_nw | abfas_nw bbfas_nw cbfas_nw dbfas_nw AT a1 a2 a3 a4;
i_bfa_mt s_bfa_mt | abfa_mt bbfa_mt cbfa_mt dbfa_mt AT a1 a2 a3 a4;

! freeing factor covs because of "nocovariances"
! But ensure they are constrained across groups
s_bfas_nw WITH i_bfas_nw (p)
s_bfa_mt (q)
i_bfa_mt (r); 
i_bfas_nw WITH s_bfa_mt (s)
i_bfa_mt (t);
s_bfa_mt WITH i_bfa_mt (u);

!constrain latent growth means across groups
[i_bfas_nw] (v); 
[s_bfas_nw] (w);
[i_bfa_mt] (x); 
[s_bfa_mt] (y);

!constrain latent growth variances across groups
i_bfas_nw (l);
s_bfas_nw (m);
i_bfa_mt (n);
s_bfa_mt (o);

p1 BY abfas_nw@1; ! Residual "factors";
p2 BY bbfas_nw@1;
p3 BY cbfas_nw@1;
p4 BY dbfas_nw@1;

v1 BY abfa_mt@1; ! Residual "factors";
v2 BY bbfa_mt@1;
v3 BY cbfa_mt@1;
v4 BY dbfa_mt@1;

!residual variances on observed/manifest vars set to 0
abfas_nw@0;
bbfas_nw@0;
cbfas_nw@0;
dbfas_nw@0;

!residual variances on observed/manifest vars set to 0
abfa_mt@0;
bbfa_mt@0;
cbfa_mt@0;
dbfa_mt@0;

! means of observed/manifest vars set to 0
[abfas_nw@0];
[bbfas_nw@0];
[cbfas_nw@0];
[dbfas_nw@0];

! means of observed/manifest vars set to 0
[abfa_mt@0];
[bbfa_mt@0];
[cbfa_mt@0];
[dbfa_mt@0];

[p1@0]; ! means of residual "factors" set to 0
[p2@0];
[p3@0];
[p4@0];

[v1@0]; ! means of residual "factors" set to 0
[v2@0];
[v3@0];
[v4@0];

p4 on p3 (a); ! Autoregressive paths constrained to equality
p3 on p2 (a);
p2 on p1 (a);

v4 on v3 (b); ! Autoregressive paths constrained to equality
v3 on v2 (b);
v2 on v1 (b);

p4 on v3 (c); ! Cross-lagged paths constrained to equality
p3 on v2 (c);
p2 on v1 (c);

v4 on p3 (d); ! Cross-lagged paths constrained to equality
v3 on p2 (d);
v2 on p1 (d);

p1 with v1; !(this one is not like the others)

p2 with v2 (e); ! Within-wave correlations constrained to equality
p3 with v3 (e);
p4 with v4 (e);

p2 p3 p4 (f); ! estimate variances of residual "factors"
v2 v3 v4 (g); ! estimate variances of residual "factors"

MODEL earlier:

p4 on p3 (a23); ! Autoregressive paths constrained to equality
p3 on p2 (a23);
p2 on p1 (a23);

v4 on v3 (b23); ! Autoregressive paths constrained to equality
v3 on v2 (b23);
v2 on v1 (b23);

p4 on v3 (c23); ! Cross-lagged paths constrained to equality
p3 on v2 (c23);
p2 on v1 (c23);

v4 on p3 (d23); ! Cross-lagged paths constrained to equality
v3 on p2 (d23);
v2 on p1 (d23);

p1 with v1; !(this one is not like the others)

p2 with v2 (e23); ! Within-wave correlations constrained to equality
p3 with v3 (e23);
p4 with v4 (e23);

p2 p3 p4 (f23); ! estimate variances of residual "factors"
v2 v3 v4 (g23); ! estimate variances of residual "factors"

MODEL later:

p4 on p3 (a45); ! Autoregressive paths constrained to equality
p3 on p2 (a45);
p2 on p1 (a45);

v4 on v3 (b45); ! Autoregressive paths constrained to equality
v3 on v2 (b45);
v2 on v1 (b45);

p4 on v3 (c45); ! Cross-lagged paths constrained to equality
p3 on v2 (c45);
p2 on v1 (c45);

v4 on p3 (d45); ! Cross-lagged paths constrained to equality
v3 on p2 (d45);
v2 on p1 (d45);

p1 with v1; !(this one is not like the others)

p2 with v2 (e45); ! Within-wave correlations constrained to equality
p3 with v3 (e45);
p4 with v4 (e45);

p2 p3 p4 (f45); ! estimate variances of residual "factors"
v2 v3 v4 (g45); ! estimate variances of residual "factors"

OUTPUT: SAMPSTAT PATTERNS;
