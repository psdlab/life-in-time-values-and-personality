[[init]]
iterators = whichP whichV;

whichP = 1:16;
whichV = 1:15;

varP#whichP =  bfas_ac  bfas_ap  bfas_ci  bfas_co  bfas_ea  bfas_ee  bfas_nv  bfas_nw  bfas_oi  bfas_oo
bfi_a6  bfi_c bfi_e bfi_hp8 bfi_n bfi_o;

varV#whichV = aspfin aspfin_d bfa_mt bfa_mt_d hrz_col hrz_col_d hrz_ind hrz_ind_d mvi usi usi_d vrt_col vrt_col_d vrt_ind vrt_ind_d;

filename="BivLCM-SR-[[varP#whichP]]_[[varV#whichV]]-PLin-VLin.inp";
outputDirectory="/home/jflournoy/code_new/lnt_pxvx/Rez/bivariate-lcmsr-post_mi/[[varP#whichP]]_[[varV#whichV]]";
[[/init]]

title: PxVx Bivariate LCM-SR - post mi - Linear [[varP#whichP]] with Linear [[varV#whichV]];
data: file = /home/jflournoy/code_new/lnt_pxvx/Data/lnt_nat_recalc.tsv;

variable:
names =
subjid
aage
aaspfin
aaspfin_d
abfa_mt
abfa_mt_d
abfas_ac
abfas_ap
abfas_ci
abfas_co
abfas_ea
abfas_ee
abfas_nv
abfas_nv9
abfas_nw
abfas_oi
abfas_oo
abfi_a
abfi_a6
abfi_c
abfi_d_scale
abfi_e
abfi_hp8
abfi_n
abfi_o
abfi_s_scale
ahrz_col
ahrz_col_d
ahrz_ind
ahrz_ind_d
amvi
ausi
ausi_d
avrt_col
avrt_col_d
avrt_ind
avrt_ind_d
bage
baspfin
baspfin_d
bbfa_mt
bbfa_mt_d
bbfas_ac
bbfas_ap
bbfas_ci
bbfas_co
bbfas_ea
bbfas_ee
bbfas_nv
bbfas_nv9
bbfas_nw
bbfas_oi
bbfas_oo
bbfi_a
bbfi_a6
bbfi_c
bbfi_d_scale
bbfi_e
bbfi_hp8
bbfi_n
bbfi_o
bbfi_s_scale
bhrz_col
bhrz_col_d
bhrz_ind
bhrz_ind_d
bmvi
busi
busi_d
bvrt_col
bvrt_col_d
bvrt_ind
bvrt_ind_d
cage
caspfin
caspfin_d
cbfa_mt
cbfa_mt_d
cbfas_ac
cbfas_ap
cbfas_ci
cbfas_co
cbfas_ea
cbfas_ee
cbfas_nv
cbfas_nv9
cbfas_nw
cbfas_oi
cbfas_oo
cbfi_a
cbfi_a6
cbfi_c
cbfi_d_scale
cbfi_e
cbfi_hp8
cbfi_n
cbfi_o
cbfi_s_scale
chrz_col
chrz_col_d
chrz_ind
chrz_ind_d
cmvi
cusi
cusi_d
cvrt_col
cvrt_col_d
cvrt_ind
cvrt_ind_d
dage
daspfin
daspfin_d
dbfa_mt
dbfa_mt_d
dbfas_ac
dbfas_ap
dbfas_ci
dbfas_co
dbfas_ea
dbfas_ee
dbfas_nv
dbfas_nv9
dbfas_nw
dbfas_oi
dbfas_oo
dbfi_a
dbfi_a6
dbfi_c
dbfi_d_scale
dbfi_e
dbfi_hp8
dbfi_n
dbfi_o
dbfi_s_scale
dhrz_col
dhrz_col_d
dhrz_ind
dhrz_ind_d
dmvi
dusi
dusi_d
dvrt_col
dvrt_col_d
dvrt_ind
dvrt_ind_d;

USEVARIABLES =
a[[varP#whichP]]
b[[varP#whichP]]
c[[varP#whichP]]
d[[varP#whichP]]
a[[varV#whichV]]
b[[varV#whichV]]
c[[varV#whichV]]
d[[varV#whichV]]
a1
a2
a3
a4
;

missing = all (-9999);

tscores = a1 a2 a3 a4;

DEFINE:
a1 = aage + 0 - 36;
a2 = aage + 1 - 36;
a3 = aage + 2 - 36;
a4 = aage + 3 - 36;

ANALYSIS:
TYPE=RANDOM;
PROCESSORS = 6; 
model=nocovariances; ! supresses default estimation of certain covs in model;

MODEL:

i_[[varP#whichP]] s_[[varP#whichP]] | a[[varP#whichP]] b[[varP#whichP]] c[[varP#whichP]] d[[varP#whichP]] AT a1 a2 a3 a4;
i_[[varV#whichV]] s_[[varV#whichV]] | a[[varV#whichV]] b[[varV#whichV]] c[[varV#whichV]] d[[varV#whichV]] AT a1 a2 a3 a4;

! freeing factor covs because of "nocovariances"
s_[[varP#whichP]] WITH i_[[varP#whichP]] s_[[varV#whichV]] i_[[varV#whichV]]; 
i_[[varP#whichP]] WITH s_[[varV#whichV]] i_[[varV#whichV]];
s_[[varV#whichV]] WITH i_[[varV#whichV]];

p1 BY a[[varP#whichP]]@1; ! Residual "factors";
p2 BY b[[varP#whichP]]@1;
p3 BY c[[varP#whichP]]@1;
p4 BY d[[varP#whichP]]@1;

v1 BY a[[varV#whichV]]@1; ! Residual "factors";
v2 BY b[[varV#whichV]]@1;
v3 BY c[[varV#whichV]]@1;
v4 BY d[[varV#whichV]]@1;

!residual variances on observed/manifest vars set to 0
a[[varP#whichP]]@0;
b[[varP#whichP]]@0;
c[[varP#whichP]]@0;
d[[varP#whichP]]@0;

!residual variances on observed/manifest vars set to 0
a[[varV#whichV]]@0;
b[[varV#whichV]]@0;
c[[varV#whichV]]@0;
d[[varV#whichV]]@0;

! means of observed/manifest vars set to 0
[a[[varP#whichP]]@0];
[b[[varP#whichP]]@0];
[c[[varP#whichP]]@0];
[d[[varP#whichP]]@0];

! means of observed/manifest vars set to 0
[a[[varV#whichV]]@0];
[b[[varV#whichV]]@0];
[c[[varV#whichV]]@0];
[d[[varV#whichV]]@0];

[p1@0]; ! means of residual "factors" set to 0
[p2@0];
[p3@0];
[p4@0];

[v1@0]; ! means of residual "factors" set to 0
[v2@0];
[v3@0];
[v4@0];

p4 on p3 (a); ! Autoregressive paths constrained to equality
p3 on p2 (a);
p2 on p1 (a);

v4 on v3 (b); ! Autoregressive paths constrained to equality
v3 on v2 (b);
v2 on v1 (b);

p4 on v3 (c); ! Cross-lagged paths constrained to equality
p3 on v2 (c);
p2 on v1 (c);

v4 on p3 (d); ! Cross-lagged paths constrained to equality
v3 on p2 (d);
v2 on p1 (d);

p1 with v1; !(this one is not like the others)

p2 with v2 (e); ! Within-wave correlations constrained to equality
p3 with v3 (e);
p4 with v4 (e);

p2 p3 p4 (f); ! estimate variances of residual "factors"
v2 v3 v4 (g); ! estimate variances of residual "factors"

OUTPUT: SAMPSTAT PATTERNS;
